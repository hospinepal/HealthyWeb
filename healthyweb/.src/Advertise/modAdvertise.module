' Gambas module file

Public $LeftAdPathLink As String
Public $RightAdPathLink As String

Public $advCon As Connection
Public AdvertiseId As Long
Public LABEL_CAPTION As String
Public $adLink As String

Private $AdvArray As String[]
Private $AdvLink As String[]

Public Sub ConnectAdLocalDB()

  Dim sPath As String

  sPath = modHelpVariable.AppDirectory &/ "advertise"
  modDBApp.CreateSQliteDB(sPath)
  $advCon = modDBApp.OpenConnection("sqlite3", File.Dir(sPath), File.Name(sPath), "", "")
  If $advCon.Opened = True Then
    modBasic.DebugString("App connected to local sqlite database")
  Endif
  CreateAdvertiseDB()

End

Public Sub CreateAdvertiseDB()

  Dim hTable As Table

  If Not $advCon.Tables.Exist("tbladvertise") Then
    hTable = $advCon.Tables.Add("tbladvertise")
    hTable.Fields.Add("advid", db.Serial)
    hTable.Fields.Add("advdate", gb.Date)
    hTable.Fields.Add("advdepartment", gb.String, 50)
    hTable.Fields.Add("advcontent", gb.String, 250)
    hTable.Fields.Add("advlink", gb.String, 250)
    hTable.PrimaryKey = ["advid"]
    hTable.Update
  Endif

  If Not $advCon.Tables.Exist("tbladlinks") Then
    hTable = $advCon.Tables.Add("tbladlinks")
    hTable.Fields.Add("advid", db.Serial)
    hTable.Fields.Add("advdate", gb.Date)
    hTable.Fields.Add("advdepartment", gb.String, 50)
    hTable.Fields.Add("advposition", gb.String, 250)
    hTable.Fields.Add("advlink", gb.String, 250)
    hTable.PrimaryKey = ["advid"]
    hTable.Update
  Endif

End

Public Sub DownLoadAdvertise()

  Dim sql As String
  Dim res As Result
  Dim xTxt As String
  Dim xURL As String

  If Application.Title = "SISH" Then
    xTxt = "THIS  IS  SOFTWARE  FOR  INTEGRATED  SERVICES  IN  HOSPITAL"
    xURL = "http://www.safiur.com.np/sish"
  Else If Application.Title = "HealthyBit" Then
    xTxt = "THIS  IS  HealthyBit  HIS"
    xURL = "http://bitnepal.safiur.com.np/healthybit/"
  Endif

  AdvertiseId = 0
  sql = "select advid,advcontent,advlink from tbladvertise where advdepartment=&1 and advdate>=&2 and advdate<=&3"
  res = $advCon.Exec(sql, modHelpVariable.$LogInGroup, modDate.StartSqlDate(Now()), modDate.EndSqlDate(Now()))
  $AdvArray = New String[]
  $AdvLink = New String[]

  $AdvArray.Add(xTxt)
  $AdvLink.Add(xURL)

  If res.Count Then
    For Each res
      $AdvArray.Add(res!advcontent)
      $AdvLink.Add(res!advlink)
    Next

  Else
    $AdvArray.Add(xTxt)
    $AdvLink.Add(xURL)

  Endif

End

Public Sub GetAdvertise()

  Dim xx As Integer

  xx = AdvertiseId + 1
  LABEL_CAPTION = " * * * * *  " & $AdvArray[xx] & "  * * * * * "
  $adLink = $AdvLink[xx]
  AdvertiseId = xx

Catch
  ErrorAdvertise()

End

Private Sub ErrorAdvertise()

  DownLoadAdvertise()
  GetAdvertise()

Catch
  modBasic.DebugString(Error.Text)

End

Public Sub GetSideAdvImage()

  Dim res1 As Result
  Dim res2 As Result

  res1 = $advCon.Exec("select advlink from tbladlinks where advdepartment=&1 and advposition=&2 and advdate>=&3 and advdate<=&4", modHelpVariable.$LogInGroup, "Left", modDate.StartSqlDate(Now()), modDate.EndSqlDate(Now()))
  If res1.Available Then
    res1.MoveLast
    If res1["advlink"] Then
      $LeftAdPathLink = res1["advlink"]
    Endif
  Endif

  res2 = $advCon.Exec("select advlink from tbladlinks where advdepartment=&1 and advposition=&2 and advdate>=&3 and advdate<=&4", modHelpVariable.$LogInGroup, "Right", modDate.StartSqlDate(Now()), modDate.EndSqlDate(Now()))
  If res2.Available Then
    res2.MoveLast
    If res2["advlink"] Then
      $RightAdPathLink = res2["advlink"]
    Endif
  Endif

End

''=========================== Get Ad content from Web ===============================
Public Sub UpdateAdvertiseContent()

  Dim xpath As String
  Dim xLinkPanel As String
  Dim xLinkfeed As String
  Dim xStrPanel As String
  Dim xStrFeed As String

  modWeb.DownloadRemoteWebConfig()
  xpath = modWeb.GetWebConfigFile()
  xLinkPanel = modControlSub.ShowSettingValue(xpath, "Advertise/PanellLink")
  xLinkfeed = modControlSub.ShowSettingValue(xpath, "Advertise/FeedLink")
  ConnectAdLocalDB()

  xStrPanel = modDevAll.GetWgetDownlaodString(xLinkPanel)
  If xStrPanel Then
    $advCon.Delete("tbladlinks")
    modTextDB.InsertJsonToSQLiteTable($advCon, "tbladlinks", xStrPanel)
  Endif

  xStrFeed = modDevAll.GetWgetDownlaodString(xLinkfeed)
  If xStrFeed Then
    $advCon.Delete("tbladvertise")
    modTextDB.InsertJsonToSQLiteTable($advCon, "tbladvertise", xStrFeed)
  Endif
  Try Kill xpath

End
