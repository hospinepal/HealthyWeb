' Gambas class file

Private $Depart As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(sDepart As String)

  $Depart = sDepart
  lbldepart.Text = $Depart
  cmboxygen.List = ["Present", "Absent"]
  cmbventilator.List = ["Present", "Absent"]
  cmbother.List = ["Present", "Absent"]
  cmbstatus.List = ["Active", "Inactive"]
  SHowDepartBed()

End

' ' Public Sub Form_KeyRelease()
' '
' '   If Key.Code = Key.Esc Then
' '     Me.Close
' '   Else
' '     modGeneralmain.GoToNextControlTab()
' '   Endif
' '
' ' End
' '
' ' Public Sub cmboxygen_KeyRelease()
' '
' '   modFillContainer.AutoFillComboBox(cmboxygen)
' '   modFillContainer.RestrictComboToContent(cmboxygen)
' '
' ' End
' '
' ' Public Sub cmbventilator_KeyRelease()
' '
' '   modFillContainer.AutoFillComboBox(cmbventilator)
' '   modFillContainer.RestrictComboToContent(cmbventilator)
' '
' ' End

Private Sub SHowDepartBed()

  Dim sql As String

  sql = "select fldbed,fldstatus,fldoxyport,fldventilator,fldother from tbldepartmentbed where flddept=&1"           ''
  $rData = modDatabase.$myConn.Exec(sql, $Depart)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "Bed No"
    .Columns[1].Text = "Status"
    .Columns[2].Text = "Oxygen"
    .Columns[3].Text = "Ventilator"
    .Columns[4].Text = "Other"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Private Sub BlankAllControl()

  txtbedno.Text = ""
  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""
  cmbstatus.Text = ""

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtbedno.Text = $rData["fldbed"]
    cmbstatus.Text = $rData["fldstatus"]
    cmboxygen.Text = $rData["fldoxyport"]
    cmbventilator.Text = $rData["fldventilator"]
    cmbother.Text = $rData["fldother"]
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If txtbedno.Text Then
    res = modDatabase.$myConn.Create("tbldepartmentbed")
    res["fldbed"] = Trim(txtbedno.Text)
    res["flddept"] = $Depart
    res["fldoxyport"] = Trim(cmboxygen.Text)
    res["fldventilator"] = Trim(cmbventilator.Text)
    res["fldother"] = Trim(cmbother.Text)
    res["fldstatus"] = cmbstatus.Text
    res["xyz"] = False
    res.Update
    SHowDepartBed()
    BlankAllControl()
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    txtbedno.SetFocus
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If txtbedno.Text Then
    res = modDatabase.$myConn.Edit("tbldepartmentbed", "fldbed=&1", Trim(txtbedno.Text))
    res["fldoxyport"] = Trim(cmboxygen.Text)
    res["fldventilator"] = Trim(cmbventilator.Text)
    res["fldother"] = Trim(cmbother.Text)
    res["fldstatus"] = cmbstatus.Text
    res["xyz"] = False
    res.Update
    SHowDepartBed()
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub mnudelete_Click()

  If txtbedno.Text Then
    modDatabase.$myConn.Delete("tbldepartmentbed", "fldbed=&1", Trim(txtbedno.Text))
    SHowDepartBed()
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    BlankAllControl()
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $Depart, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btnclose_Click()

  Me.Close()

End
