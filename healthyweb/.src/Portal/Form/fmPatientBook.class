' Gambas class file

Private $FileID As String
Private $sUser As String
Private $sFormat As String
Private $rData As Result
Private $aMyFields As String[]
Private $selDate As Boolean

Private $billno As String
Private $sToken As String
Private $HashCode As String

Public Sub _new(sUser As String, sFormat As String)

  $sUser = sUser
  $sFormat = sFormat

  cmbsex.List = ["Female", "Male", "Other"]
  cmbsex.Text = ""
  chknull.Value = False

  SelectHospitalID()
  LoadBasicLists()

  dtconsultdate.Value = DateAdd(Date(), gb.Day, 1)
  txtnepconsult.Text = modDate.ConvertToLocaldate(dtconsultdate.Value)
  dtconsultime.Text = ""
  btnyear.Text = "Year"
  btnmon.Text = "Mon"
  rbself.Value = True
  ShowBookTable()
  PastMessage()

End

Private Sub LoadBasicLists()

  cmbdistrict.List = modBasic.$DistrictList
  cmbrelation.List = modBasic.$RelationList

  cmbdisctype.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldtype from tbldiscount where fldonline=&1", "Enable"))
  cmbconsult.List = modGeneral.RegistrationDeptList()

End

''=========== Select Hospital ==============
Private Sub SelectHospitalID()

  Dim xlst As String

  If MMain.$WebEntry = True Then
    If modBasic.$HospCode Then
      cmbhospital.Text = modBasic.$HospCode
      txthospital.Text = modDataRepo.GetHospitaltName(modBasic.$HospCode)
      cmbhospital.Enabled = False
    Else
      xlst = modSettings.ShowSettingFromFIle("OnlineBooking/BookingHospitals")
      If xlst Then
        cmbhospital.List = Split(xlst, ";")
      Endif
    Endif
  Else
    pnlhospname.Visible = False
  Endif

End

Public Sub cmbhospital_Click()

  If cmbhospital.Text Then
    txthospital.Text = modDataRepo.GetHospitaltName(cmbhospital.Text)
    If txthospital.Text Then
      modBasic.$HospCode = cmbhospital.Text
    Endif
  Endif

End

''==================== Grid View ============
Private Sub PastMessage()

  Dim xcount As Integer

  xcount = 0
  For Each $rData
    If Not $rData["fldbillno"] And If $rData["fldstate"] <> "Cancelled" Then
      xcount = xcount + 1
    Endif
  Next

  If xcount Then
    lblstatus.Text = CStr(xcount) & " Consultations are Pending"
  Endif

End

Private Sub ShowBookTable()

  Dim sql As String

  If MMain.$WebEntry = True And If modBasic.$HospCode Then
    sql = "select fldbookingval,fldconsultdate,flddisctype,fldadmitlocat,fldstate,flduserid,fldhospital,fldpatientval,fldpayreference,fldbillno,fldhashcode,flditemamt from tblonlinebook where fldorduserid=&1 and fldhospital=&2 ORDER BY fldptadmindate DESC"
  Else
    sql = "select fldbookingval,fldconsultdate,flddisctype,fldadmitlocat,fldstate,flduserid,fldhospital,fldpatientval,fldpayreference,fldbillno,fldhashcode,flditemamt from tblonlinebook where fldorduserid=&1 ORDER BY fldptadmindate DESC"
  Endif
  $rData = modDatabase.$myConn.Exec(sql, $sUser, modBasic.$HospCode)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[10].Hidden = True
    .Columns[11].Hidden = True

    .Columns[0].Text = "BookID"
    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Scheme"
    .Columns[3].Text = "Department"
    .Columns[4].Text = "Status"
    .Columns[5].Text = "Consultant"
    .Columns[7].Text = "PatientID"
    .Columns[9].Text = "Invoice"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 5 Then
    Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $rData["fldstate"] = "Planned" Then
      txtbookingid.Text = $rData["fldbookingval"]
      txtamount.Value = $rData["flditemamt"]
      txthash.Text = $rData["fldhashcode"]
    Endif
  Endif

End

''================== Buttons ==================
Public Sub btnOK_Click()

  If cmbconsult.Text And If cmbdisctype.Text Then
    If txtreference.Text = "Claim Code" Then
      txtbookingid.Text = GetPatientBookingCredit()
    Else
      txtbookingid.Text = GetPatientBookingPaid()
    Endif
  Endif

  If txtbookingid.Text Then
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ShowBookTable()
    btnOK.Enabled = False
  Endif

End

Public Sub rbother_Click()

  If rbother.Value = True Then
    pnlpatient.Visible = True
  Else If rbself.Value = True Then
    pnlpatient.Visible = False
  Endif

End

Private Function GetPatientBookingPaid() As String

  Dim res As Result
  Dim res1 As Result
  Dim sql As String
  Dim xdate As Date
  Dim $DOBirth As Date
  Dim xbookID As String
  Dim xformat As String

  xformat = modGeneral.GetCategoryFromDept(cmbconsult.Text)
  If dtconsultime.Text Then
    xdate = dtconsultdate.Value
  Else
    xdate = DateAdd(Now(), gb.Day, 1)
  Endif
  $HashCode = modPassword.GetRandomPassword()
  txtamount.Value = modBillings.GetAutoRegistCost(cmbconsult.Text, cmbdisctype.Text, cmbbillmode.Text, "New")

  modDatabase.$myConn.Begin
  xbookID = modBillLock.BookingNoValue()
  If xbookID Then

    res = modDatabase.$myConn.Create("tblonlinebook")
    If rbself.Value = True Then
      If modBasic.$lbluser Then
        sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldemail,fldptcode,fldrelation,fldencrypt from tblpatientinfo where fldpatientval=&1"
        res1 = modDatabase.$myConn.Exec(sql, modBasic.$lbluser)
      Else
        If $sFormat = "RepoPortal" Then
          sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldemail,fldptcode,fldrelation,fldencrypt from tblremoteusers where fldusrcode=&1"
          res1 = modDatabase.$myConn.Exec(sql, $sUser)
        Endif
      Endif
      If res1.Available Then
        res["fldpatientval"] = modBasic.$lbluser
        res["fldptnamefir"] = modPassword.DecryptPatData(res1["fldptnamefir"], res1["fldencrypt"])
        res["fldptnamelast"] = modPassword.DecryptPatData(res1["fldptnamelast"], res1["fldencrypt"])
        res["fldethniccode"] = res1["fldethniccode"]
        res["fldptaddvill"] = res1["fldptaddvill"]
        res["fldptaddward"] = res1["fldptaddward"]
        res["fldptadddist"] = res1["fldptadddist"]
        res["fldptsex"] = res1["fldptsex"]
        res["fldptcontact"] = modPassword.DecryptPatData(res1["fldptcontact"], res1["fldencrypt"])
        res["fldptguardian"] = res1["fldptguardian"]
        res["fldrelation"] = res1["fldrelation"]
        res["fldptbirday"] = res1["fldptbirday"]
        res["fldptadmindate"] = Now()
        res["fldemail"] = modPassword.DecryptPatData(res1["fldemail"], res1["fldencrypt"])
        res["fldptcode"] = res1["fldptcode"]
      Endif

    Else
      If $selDate = True Then
        $DOBirth = dtdobmain.Tag
      Else
        If txtmonths.Value = 0 Then
          If btnyear.Text = "Year" Then
            $DOBirth = DateAdd(Now(), gb.Year, 0 - CInt(txtyears.Value))
          Else If btnyear.Text = "Mon" Then
            $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(txtyears.Value))
          Else If btnyear.Text = "Day" Then
            $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(txtyears.Value))
          Endif

        Else
          If btnyear.Text = "Year" And If btnmon.Text = "Mon" Then
            $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(12 * txtyears.Value + txtmonths.Value))
          Else If btnyear.Text = "Mon" And If btnmon.Text = "Day" Then
            $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(30 * txtyears.Value + txtmonths.Value))
          Else If btnyear.Text = "Day" And If btnmon.Text = "Hour" Then
            $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(24 * txtyears.Value + txtmonths.Value))
          Endif

        Endif
      Endif
      res["fldpatientval"] = Trim(txtpatno.Text)
      res["fldptnamefir"] = Trim(txtnamefir.text)
      res["fldptnamelast"] = Trim(txtnamelast.text)
      res["fldethniccode"] = Trim(txtethcode.Text)
      res["fldptaddward"] = Trim(txtward.Text)
      res["fldptadddist"] = String.UCaseFirst(Trim(txtaddfir.Text))
      res["fldptsex"] = cmbsex.text
      res["fldptcontact"] = Trim(txtcontact.Text)
      res["fldptguardian"] = Trim(txtguardian.Text)
      res["fldrelation"] = Trim(cmbrelation.Text)
      If chknull.Value = False Then
        res["fldptbirday"] = $DOBirth
      Endif
      res["fldptadmindate"] = Now()
      res["fldemail"] = Trim(txtemail.Text)
      res["fldptcode"] = Trim(txtpatcode.Text)

    Endif
    res["fldbookingval"] = xbookID
    res["fldconsultdate"] = xdate
    res["fldadmitlocat"] = cmbconsult.Text
    res["flduserid"] = btnconsult.Tag
    res["fldstate"] = "Planned"
    res["flddisctype"] = cmbdisctype.Text
    res["fldbillingmode"] = cmbbillmode.Text
    res["fldcomp"] = modBasic.$compID
    res["fldorduserid"] = $sUser
    res["fldhashcode"] = $HashCode
    res["flditemamt"] = txtamount.Value
    res["fldpayreference"] = ""
    res["fldhospital"] = modBasic.$HospCode
    res["fldcomment"] = ""
    res["fldencounterval"] = ""
    res["fldbillno"] = ""
    If MMain.$WebEntry = True Then
      res["fldrepodate"] = Now()
      res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update()

    modDatabase.$myConn.Commit
    Return xbookID
    Me.Exec("Toastify({text: 'Booking Completed', duration: 3000}).showToast()")
  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End

Private Function GetPatientBookingCredit() As String

  Dim res As Result
  Dim res1 As Result
  Dim sql As String
  Dim dt As Date
  Dim xbookID As String
  Dim xnhis As String

  Dim hCform As CimisAPIPatient
  Dim hCElig As CimisAPIEligible
  Dim xmessage As String
  Dim xname As String
  Dim xsurname As String
  Dim xgender As String
  Dim xdob As Date
  Dim xcontact As String
  Dim xemail As String

  Dim xPolicy As String
  Dim xexpdate As Date
  Dim xcredamt As Float

  Dim aFirname As String
  Dim aLstName As String

  If rbself.Value = True Then
    If modBasic.$lbluser Then
      sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldemail,fldptcode,fldrelation,fldencrypt from tblpatientinfo where fldpatientval=&1"
      res1 = modDatabase.$myConn.Exec(sql, modBasic.$lbluser)
      If res1.Available Then
        aFirname = modPassword.DecryptPatData(res1["fldptnamefir"], res1["fldencrypt"])
        aLstName = modPassword.DecryptPatData(res1["fldptnamelast"], res1["fldencrypt"])
        xnhis = res1["fldptcode"]
      Endif
    Endif
  Else
    aFirname = Trim(txtnamefir.Text)
    aLstName = Trim(txtnamelast.Text)
    xnhis = Trim(txtpatcode.Text)
  Endif

  If xnhis Then
    hCform = New CimisAPIPatient(xnhis)
    xname = hCform.GetPatientName()
    xsurname = hCform.GetPatientSurName()
    xgender = hCform.GetPatientGender()
    xcontact = hCform.GetPatientContact()
    xemail = hCform.GetPatientEmail()
    xdob = hCform.GetPatientDateBirth()

    If xname Then
      hCElig = New CimisAPIEligible(xnhis)
      xPolicy = hCElig.GetPolicyNo()
      xexpdate = hCElig.GetexpiryDaye()
      xcredamt = hCElig.GetAllowedAmt()

      xmessage = "<b>Policy No:</b> " & xPolicy & "<br>" & "<b>Expiry:</b> " & modReportVar.GetDateTimeReport(xexpdate, gb.MediumDate) & "<br>" & "<b>Credit Amt:</b> " & modReportVar.GetLocaleNumberFormat(xcredamt, gb.Currency)
      If xexpdate < modDate.StartSqlDate(Now()) Then
        xmessage = xmessage & "<h3> Account Expired </h3>"
      Endif
      If xcredamt < 200 Then
        xmessage = xmessage & "<h4> No Balance</h4>"
      Else If xcredamt < 1000 Then
        xmessage = xmessage & "<h4> Low Balance</h4>"
      Endif
      WebHtml1.Html = xmessage

      If xexpdate > Now() Then
        If xcredamt > 200 Then
          If xname = aFirname And If xsurname = aLstName Then

            $HashCode = modPassword.GetRandomPassword()
            modDatabase.$myConn.Begin
            xbookID = modBillLock.BookingNoValue()
            If xbookID Then
              dt = Date(Year(dtconsultdate.Value), Month(dtconsultdate.Value), Day(dtconsultdate.Value), Hour(CDate(dtconsultime.Text)), Minute(CDate(dtconsultime.Text)), Second(CDate(dtconsultime.Text)))
              res = modDatabase.$myConn.Create("tblonlinebook")
              res["fldbookingval"] = xbookID
              res["fldpatientval"] = modBasic.$lbluser
              res["fldptnamefir"] = aFirname
              res["fldptnamelast"] = aLstName
              res["fldethniccode"] = res1["fldethniccode"]
              res["fldptaddvill"] = res1["fldptaddvill"]
              res["fldptaddward"] = res1["fldptaddward"]
              res["fldptadddist"] = res1["fldptadddist"]
              res["fldptsex"] = res1["fldptsex"]
              res["fldptcontact"] = modPassword.DecryptPatData(res1["fldptcontact"], res1["fldencrypt"])
              res["fldptguardian"] = res1["fldptguardian"]
              res["fldrelation"] = res1["fldrelation"]
              res["fldptbirday"] = res1["fldptbirday"]
              res["fldptadmindate"] = Now()
              res["fldemail"] = modPassword.DecryptPatData(res1["fldemail"], res1["fldencrypt"])
              res["fldptcode"] = xnhis
              res["fldconsultdate"] = dt
              res["fldadmitlocat"] = cmbconsult.Text
              res["flduserid"] = btnconsult.Tag
              res["fldstate"] = "Planned"
              res["flddisctype"] = cmbdisctype.Text
              res["fldbillingmode"] = cmbbillmode.Text
              res["fldcomp"] = modBasic.$compID
              res["fldorduserid"] = $sUser
              res["fldhashcode"] = $HashCode
              res["flditemamt"] = 0
              res["fldpayreference"] = ""
              res["fldhospital"] = modBasic.$HospCode
              res["fldcomment"] = ""
              res["fldencounterval"] = ""
              res["fldbillno"] = ""
              If MMain.$WebEntry = True Then
                res["fldrepodate"] = Now()
                res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
                res["fldhospcode"] = modBasic.$HospCode
              Endif
              res.Update()
            Endif
            modDatabase.$myConn.Commit
            Return xbookID
            Me.Exec("Toastify({text: 'Booking Completed', duration: 3000}).showToast()")

          Else
            Me.Exec("Toastify({text: 'Account doesnot match', duration: 3000}).showToast()")
          Endif
        Else
          Me.Exec("Toastify({text: 'Account Less than Rs 200', duration: 3000}).showToast()")
        Endif
      Else
        Me.Exec("Toastify({text: 'Account Expired', duration: 3000}).showToast()")
      Endif

    Endif
  Else
    Me.Exec("Toastify({text: 'NHIS missing', duration: 3000}).showToast()")
  Endif

End

''-------------------- Patient Profile Entry -----------------------------
Public Sub btnCancel_Click()

  txtbookingid.Text = ""
  txtamount.Value = 0
  lblstatus.Text = ""
  WebProgressBar1.Value = 0
  btnpay.Enabled = True
  btnOK.Enabled = True
  txtnamefir.Password = False
  txtcontact.Password = False
  txtemail.Password = False

End

Public Sub btnrefresh_Click()

  Dim sql As String
  Dim res As Result

  If txtpatno.Text Then
    sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldemail,fldptcode,fldrelation,fldencrypt from tblpatientinfo where fldpatientval=&1"
    res = modDatabase.$myConn.Exec(sql, Trim(txtpatno.Text))
    If res.Available Then
      txtnamefir.Password = True
      txtcontact.Password = True
      txtemail.Password = True

      txtnamefir.text = res["fldptnamefir"]
      txtnamelast.text = res["fldptnamelast"]
      txtethcode.Text = res["fldethniccode"]
      txtward.Text = res["fldptaddward"]
      txtaddfir.Text = res["fldptaddvill"]
      cmbdistrict.Text = res["fldptadddist"]
      cmbsex.text = res["fldptsex"]
      txtcontact.Text = res["fldptcontact"]
      txtguardian.Text = res["fldptguardian"]
      cmbrelation.Text = res["fldrelation"]
      dtdobmain.Tag = res["fldptbirday"]
      $selDate = True
      txtemail.Text = res["fldemail"]
      txtpatcode.Text = res["fldptcode"]
    Endif
  Endif

End

Public Sub cmbdistrict_Click()

  If txtaddfir.List.Count Then
    txtaddfir.Clear()
  Endif
  If cmbdistrict.Text Then
    txtaddfir.List = modDataRepo.GetMunicipalsByDistrict(cmbdistrict.Text)
  Endif

End

Public Sub btnyear_Click()

  If btnyear.Text = "Year" Then
    btnyear.Text = "Mon"
    btnmon.Text = "Day"
  Else If btnyear.Text = "Mon" Then
    btnyear.Text = "Day"
    btnmon.Text = "Hour"
  Else If btnyear.Text = "Day" Then
    btnyear.Text = "Year"
    btnmon.Text = "Mon"
  Endif

End

Private Sub DateBoxEntry()

  If txtyears.Value = 0 And If txtmonths.Value = 0 Then
    chknull.Value = True
  Else
    chknull.Value = False
  Endif

  If txtmonths.Value > 12 And If btnmon.Text = "Mon" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 31 And If btnmon.Text = "Day" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 24 And If btnmon.Text = "Hour" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Endif

End

Public Sub txtyears_Activate()

  DateBoxEntry()

End

Public Sub txtmonths_Activate()

  DateBoxEntry()

End

Public Sub dtdobmain_Click()

  Dim xdate As Date

  xdate = GetDateValue(("Select Date Time of Birth"), "DateTime", dtdobmain.Tag)
  If xdate Then
    dtdobmain.Tag = xdate
  Endif
  If dtdobmain.Tag Then
    $selDate = True
    modDateSub.DateToYearandMonth(dtdobmain.Tag, txtyears, txtmonths, btnyear, btnmon)
  Endif

End

Public Sub btnnepdate_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtdobmain.Tag))
  If xx Then
    dtdobmain.Tag = modDate.ConvertToEnglishdate(xx)
  Endif
  If dtdobmain.Tag Then
    $selDate = True
    modDateSub.DateToYearandMonth(dtdobmain.Tag, txtyears, txtmonths, btnyear, btnmon)
  Endif

End

''========================== General Procedure ====================
Public Sub cmbdisctype_Select()

  LoadDiscCreditVal()

End

Private Sub LoadDiscCreditVal()

  Dim resx As Result

  txtreference.Text = ""
  txtdiscnt.Value = 0
  txtcredit.Value = 0

  If cmbdisctype.Text Then
    resx = modDatabase.$myConn.Exec("select fldbillingmode,fldamount,fldcredit,fldreference,fldlimit,fldyear from tbldiscount where fldtype=&1", cmbdisctype.Text)
    If resx.Available Then
      ''billingmode
      If resx["fldbillingmode"] Then
        cmbbillmode.Text = resx["fldbillingmode"]
      Else
        cmbbillmode.Text = modBasic.$RegistDefaultMode
      Endif
      If cmbbillmode.Text Then
        WebContainer7.Enabled = True
      Endif
      ''claim code
      If resx["fldreference"] Then
        If resx["fldreference"] = "Claim Code" Then
          txtreference.Text = "Claim Code"
        Else If resx["fldreference"] = "Accident SSF" Then
          txtreference.Text = "Accident SSF"
        Else If resx["fldreference"] = "Medical SSF" Then
          txtreference.Text = "Medical SSF"
        Endif
      Endif
      ''credit/discount limit
      If resx["fldlimit"] Then
        If resx["fldlimit"] = "YearlyDiscount" Then
          If resx["fldamount"] Then
            txtdiscnt.Value = resx["fldamount"]
          Endif
        Else If resx["fldlimit"] = "YearlyCreditAMT" Then
          If resx["fldcredit"] Then
            txtcredit.Value = resx["fldcredit"]
          Endif
        Endif
      Endif

    Endif
  Endif

End

Private Function FillCOnsultantList() As Variant[]

  Dim doclist As Variant[]

  If modBasic.$LockConsultant = "LockByQuota" Then
    doclist = modConsult.GetUserPostingList(cmbconsult.Text, dtconsultdate.Value, cmbbillmode.Text)
  Else
    doclist = modBasic.$OnlineUserList
  Endif
  Return doclist

End

Public Sub btnselectuser_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), FillCOnsultantList())
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Endif

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub dtconsultdate_Change()

  txtnepconsult.Text = modDate.ConvertToLocaldate(dtconsultdate.Value)

End

Public Sub txtnepconsult_Change()

  If Len(txtnepconsult.Text) = 10 Then
    dtconsultdate.Value = modDate.ConvertToEnglishdate(txtnepconsult.Text)
  Endif

End

''-------------- Payment ------------------
Public Sub btnpay_Click()

  Dim xpay As String
  Dim xamt As Float
  Dim res As Result
  Dim hClass As GetOnlinePay
  Dim gClass As GetFonePay

  Dim xurl As String
  Dim yurl As String
  Dim xmerch As String
  Dim xsecKey As String

  If MMain.$WebEntry = True And If modBasic.$HospCode Then
    xurl = modSettings.ShowSettingFromFIle("OnlineBooking_" & modBasic.$HospCode & "/RequestURL")
  Else
    xurl = modSettings.ShowSettingFromFIle("OnlineBooking/RequestURL")
  Endif
  If MMain.$WebEntry = True And If modBasic.$HospCode Then
    yurl = modSettings.ShowSettingFromFIle("OnlineBooking_" & modBasic.$HospCode & "/ReturnURL")
  Else
    yurl = modSettings.ShowSettingFromFIle("OnlineBooking/ReturnURL")
  Endif
  If MMain.$WebEntry = True And If modBasic.$HospCode Then
    xmerch = modSettings.ShowSettingFromFIle("OnlineBooking_" & modBasic.$HospCode & "/Merchant")
  Else
    xmerch = modSettings.ShowSettingFromFIle("OnlineBooking/Merchant")
  Endif
  If MMain.$WebEntry = True And If modBasic.$HospCode Then
    xsecKey = modSettings.ShowSettingFromFIle("OnlineBooking_" & modBasic.$HospCode & "/SecretKey")
  Else
    xsecKey = modSettings.ShowSettingFromFIle("OnlineBooking/SecretKey")
  Endif

  If txtbookingid.Text Then
    xamt = txtamount.Value
    If xamt > 0 Then

      $billno = ""
      $sToken = ""
      If modBasic.$HospCode Then
        $billno = "WEB-" & CStr(modBillLock.CurrentWebPayment()) & modBasic.$HospCode
      Else
        $billno = "WEB-" & CStr(modBillLock.CurrentWebPayment())
      Endif

      If $billno Then
        ''For Khalti
        If modPatPortal.$PaymentClient = "Khalti" Then
          $FileID = txtbookingid.Text & "-" & modString.GetDateString(Now())
          res = modDatabase.$myConn.Edit("tblpatientbook", "fldbookingval=&1", txtbookingid.Text)
          If res.Available Then
            xpay = WebPayment(txtbookingid.Text, $billno, "Booking", $FileID, xamt)
            If xpay = "Successful" Then
              res["fldstate"] = "Paid"
              res["fldpayreference"] = $billno
              res["fldcomment"] = "Khalti Payment: " & Format(xamt, gb.Fixed)
              res["xyz"] = False
              res.Update
              WebProgressBar1.Value = 1
              lblstatus.Text = "Transaction Completed"
              ShowBookTable()
            Endif
          Endif

          ''for fonepay
        Else If modPatPortal.$PaymentClient = "fonepay" Then
          If $sFormat = "RepoPortal" Then
            gClass = New GetFonePay(txtbookingid.Text, $billno, xamt, xurl, yurl, xmerch, xsecKey, $sUser, txthash.Text)
          Else
            gClass = New GetFonePay(txtbookingid.Text, $billno, xamt, xurl, yurl, xmerch, xsecKey, modBasic.$lbluser, txthash.Text)
          Endif

          ''for Chanakya
        Else If modPatPortal.$PaymentClient = "Chanakya" Then
          $sToken = modApplication.GetMD5SumString($billno)
          hClass = New GetOnlinePay(txtbookingid.Text, $billno, xamt, xurl, yurl, txthash.Text)
          xpay = hClass.GetBookingStatus()
          If xpay = "Pending" Then
            WebProgressBar1.Value = 0.25
            lblstatus.Text = "Please Wait . ."
            WebTimer1.Enabled = True
            btnpay.Enabled = False
          Endif
        Endif
      Endif

    Endif
  Endif

End

''for Chanakya testing
Public Sub WebTimer1_Timer()

  Dim xpay As String
  Dim res As Result

  xpay = modOnlineDbase.GetTokenStatus($sToken, txtbookingid.Text, $billno)
  If xpay = "Successful" Then
    res = modDatabase.$myConn.Edit("tblonlinebook", "fldbookingval=&1", txtbookingid.Text)
    If res.Available Then
      WebProgressBar1.Value = 1
      WebTimer1.Enabled = False
      res["fldstate"] = "Paid"
      res["fldpayreference"] = $billno
      res["fldcomment"] = "Chanakya Payment"
      res.Update
      lblstatus.Text = "Transaction Completed"
      ShowBookTable()
      modOnlineDbase.$OnlineCon.Close()
    Endif
  Endif

End

'''------------------------ Analyze http_referrer -------------------
Private Sub GetReferValues()

  Dim xx As String
  Dim yy As String

  Dim CForm As CGetFoneResponse

  xx = modPatPortal.$PaymentReferred
  If InStr(xx, "?") > 0 Then
    yy = Split(xx, "?")[1]

    ''fonepay
    CForm = New CGetFoneResponse(yy)

  Endif

End
