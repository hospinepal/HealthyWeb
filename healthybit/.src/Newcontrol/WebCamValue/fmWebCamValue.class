' Gambas class file

Private myPlayer As New MediaPlayer As "Player"

Private $sDevice As String
Private $sValue As String

Public Sub Run(sDevice As String) As String

  If sDevice Then
    $sDevice = sDevice
  Else
    $sDevice = "/dev/v4l/by-id/" & Dir("/dev/v4l/by-id", "*")[0]
  Endif
  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  Me.Center
  ReadyForImage()

End

Private Sub ReadyForImage()

  With myPlayer
    If .State = Media.Playing Then
      .Stop
    Endif
    .URL = "v4l2://" & $sDevice
    .SetWindow(dwgVideo)
    .Play()
  End With
  Try dwgVideo.Resize(myPlayer.Video.Image.Width, myPlayer.Video.Image.Height)

End

Public Sub ZoomSlider1_Change()

  ImageView1.Zoom = ZoomSlider1.Value / 100

End

Public Sub Form_Close()

  If myPlayer.State = Media.Playing Then myPlayer.Stop

End

Public Sub BtnTakeShot_Click()

  Dim ximage As Image
  Dim sTemp As String

  If myPlayer.State <> Media.Playing Then Return

  If modBasic.$CamGrayScale = "Black/White" Then
    ximage = myPlayer.Video.Image
    sTemp = Temp() & ".png"
    ximage.Save(sTemp)
    ximage = Image.Load(modDevAll.ConvertToBlackInWhite(sTemp))
  Else If modBasic.$CamGrayScale = "GrayColor" Then
    ximage = myPlayer.Video.Image.Desaturate()
  Else If modBasic.$CamGrayScale = "Composite Color" Then
    ximage = myPlayer.Video.Image.Picture.Image
    If modBasic.$CamOpacity Then
      ximage = ximage.Opacity(modBasic.$CamOpacity / 100)
    Endif
    If modBasic.$CamThreshold Then
      ximage.Threshold(modBasic.$CamThreshold / 100)
    Endif
  Else
    ximage = myPlayer.Video.Image
  Endif

  ImageView1.Image = ximage
  ImageView1.Zoom = ZoomSlider1.Value / 100

End

Public Sub btnsave_Click()

  Dim sPath As String

  If IsNull(ImageView1.Image) Then Return

  If modBasic.$CamImageFormat Then
    sPath = Temp() & "." & modBasic.$CamImageFormat
  Else
    sPath = Temp() & ".png"
  Endif
  If modBasic.$CamImageQuality Then
    ImageView1.Image.Save(sPath, modBasic.$CamImageQuality)
  Else
    ImageView1.Image.Save(sPath)
  Endif
  Wait

  $sValue = sPath
  Me.Close(True)

End

Public Sub btnleft_Click()

  Dim aVal As Integer

  aVal = ZoomSlider1.Value
  With ImageView1
    .Image = ImageView1.Image.RotateLeft()
    .Zoom = aVal / 100
  End With

End

Public Sub btnright_Click()

  Dim aVal As Integer

  aVal = ZoomSlider1.Value
  With ImageView1
    .Image = ImageView1.Image.RotateRight()
    .Zoom = aVal / 100
  End With

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Left Then
    btnleft_Click()
  Else If Key.Code = Key.Right Then
    btnright_Click()
  Endif

End
