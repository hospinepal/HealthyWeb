' Gambas class file

Private $categ As String
Private $frm As String

Public Sub _new(frm As String, strTest As String, strSubTest As String, sType As String)

  $frm = frm
  lbltest.Text = strTest
  lblsubtest.Text = strSubTest
  $categ = sType

End

Public Sub Form_Open()

  modGeneralMain.ArrangeFormCentre(Me)
  DisplayBlobData()

End

Public Sub Form_Close()

  modGeneralMain.RecordFormExit(Me)

End

Public Sub Form_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.Close
  Endif

End

Public Sub btnOpen_Click()

  Dialog.Filter = ["*.jpg;*.jpeg;*.png;*.bmp", "Picture files"]
  If Dialog.OpenFile() Then Return
  PictureBox1.Tag = Dialog.Path
  PictureBox1.Image = Image.Load(Dialog.Path)

End

Public Sub btnwebcam_Click()

  PictureBox1.Tag = modImage.GetWebCamValue(modDevice.WebCamDevice())
  If PictureBox1.Tag Then
    PictureBox1.Image = Image.Load(PictureBox1.Tag)
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result
  Dim res1 As Result
  Dim sPath As String
  Dim xblobfile As String

  If PictureBox1.Tag Then
    sPath = PictureBox1.Tag
    res1 = modDatabase.$medConn.Exec("select fldid from tblmedimage where fldcateg=&1 and fldname=&2", $categ, lbltest.Text & ":" & lblsubtest.Text)
    If res1.Available Then
      res = modDatabase.$medConn.Edit("tblmedimage", "fldid=&1", res1!fldid)
    Else
      res = modDatabase.$medConn.Create("tblmedimage")
      res["fldcateg"] = $categ
      res["fldname"] = lbltest.Text & ":" & lblsubtest.Text
    Endif

    If modMisc.BlobTarget("MedicalImages") = "FTP" Then
      xblobfile = modFTPSub.SendBlobToFTP(sPath, $categ, "MedicalImages")
    Else
      xblobfile = File.Load(sPath)
    Endif
    If xblobfile Then
      If modMisc.BlobTarget("MedicalImages") = "FTP" Then
        res["fldpic"] = ""
        res["fldlink"] = xblobfile
      Else
        res["fldpic"] = xblobfile
        res["fldlink"] = ""
      Endif
      res["flddet"] = ""

      res.Update
      Balloon.Info(("Data saved"), btnOK)
      Balloon.Delay = modBasic.$BalloonDelay
    Endif

  Endif

End

Private Sub DisplayBlobData()

  Dim sPath As String

  sPath = modImage.DisplayVisualData(lbltest.Text, lblsubtest.Text, $categ)
  If sPath Then
    PictureBox1.Tag = sPath
    PictureBox1.Image = Image.Load(sPath)
    PictureBox1.Zoom = Slider1.Value / 100
  Endif

End

Public Sub btnimgedit_Click()

  If PictureBox1.Tag Then
    Desktop.Open(PictureBox1.Tag)
  Endif

End

Public Sub btnclear_Click()

  If PictureBox1.Tag Then
    PictureBox1.Image.Clear()
    PictureBox1.Refresh()
    Wait
  Endif

End

Public Sub Slider1_Change()

  PictureBox1.Zoom = Slider1.Value / 100

End
